plugins {
    id "java"
    id "application"
}

def timefoldVersion = "999-SNAPSHOT"
def logbackVersion = "1.5.3"
def junitJupiterVersion = "5.10.2"
def assertjVersion = "3.25.3"

group = "org.acme"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform("ai.timefold.solver:timefold-solver-bom:${timefoldVersion}")

    implementation "ai.timefold.solver:timefold-solver-core"
    runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"

    // Testing
    testImplementation "ai.timefold.solver:timefold-solver-test"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"

}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
}

compileTestJava {
    options.encoding = "UTF-8"
}

application {
    mainClass = "org.acme.schooltimetabling.TimetableApp"
}

test {
    // Log the test execution results.
    testLogging {
        events "passed", "skipped", "failed"
    }

    useJUnitPlatform()
}
